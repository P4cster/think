
# 📄 `COUNTIF()`

## 🔹 Syntax
`=COUNTIF(range, criteria)`

### Parameter

| Parameter | Beschreibung                                | Pflichtangabe | erwarteter Datentyp                          |
| --------- | ------------------------------------------- | ------------- | -------------------------------------------- |
| range     | Der Bereich welcher durchlaufen werden soll | Ja            | Bereich, (kein berechnetes) Array, Zelle     |
| criteria  | Bedingung, auf die überprüft wird           | Ja            | Bedingung, die in booleschem Wert resultiert |

## 🔍 In einfacher Sprache
*Was macht diese Funktion eigentlich – ohne Fachchinesisch?*
> Die `COUNTIF` kann immer dann verwendet werden, wenn man einen, oder mehrere Bereiche, auf eine Bedingung überprüfen und sich die Anzahl zurückgeben lassen möchte, bei der der jeweilige Zellwert innerhalb des Bereichs die Bedingung erfüllt hat. 
> Klingt komplizierter als es ist. Letztlich nicht anderes als "Wie viele Datensätze erfüllen welche Bedingung?"
> Stell dir einfach vor du bist Lehrer und jemand fragt dich, wie viele Kinder in deiner Klasse über 1,60m groß sind. Der "Bereich" der durchsucht werden soll ist die Größenangabe der Kinder. Die Bedingung ist "größer als 1,60m".

## Wichtig zu wissen!
Bei der `COUNTIF` und der `SUMIF` sind Bedingungsangaben leider nicht sonderlich intuitiv. Stellen wir uns vor, wir hätten einen Bereich von 1 - 15, in dem gezählt werden soll, wie viele Zahlen größer als 10 sind. Das Ergebnis, welches wir erwarten ist logischerweise 5. Möchten wir unsere Formel nun schreiben, sähe diese intuitiv wie folgt aus: `=COUNTIF(A1:A15, >10)`. BOOM. Fehler! Bzw. lässt uns Excel die Formel nichtmals bestätigen. Aber warum?

Um diese Frage zu beantworten muss man wissen, wie Excel innerhalb dieser Formeln "liest". Der Fehler liegt hier bei der "größer als" Angabe. Da hier an erster Stelle keine Zahl übergeben wird und auch nichts in Anführungsstrichen geschrieben wurde, geht Excel davon aus, dass es sich um einen Fehler handelt. Ein Text, der nicht als Text deklariert wurde. Und da hat Excel auch absolut recht. Wenn auch nicht intuitiv.

Um nun Bedingungen innerhalb der `COUNTIF` und `SUMIF` so übergeben zu können, dass Excel das Ganze auch interpretieren kann, müssen wir uns eines kleinen Tricks bedienen. Wir verwenden die Excelinterne "Bau mir daraus einen Text"-Funktionalität, und übergeben die Bedingung mit Anführungszeichen

`=COUNTIF(A1:A15, ">10")`

Und hier kommt unser gewünschtes Ergebnis von 5 raus.

Möchte man das ganze nun mit einem variablen Wert verbinden muss zwischen Bedingungsoperator und Prüfwert ein und-Zeichen (`&`) verwendet werden.

`=COUNTIF(A1:A15, ">"&A1)`

Absolut gar nicht intuitiv. Muss man dementsprechend wissen und umgehen können.

Und nur um die Brücke zu schlagen: Diese `COUNTIF` macht nun das gleiche wie eine `=SUM(--(A1:A15>10))`. Jenachdem wie versiert man im Lesen von Excelformeln ist, bietet sich zur intuitiveren Darstellung die `SUM`-Schreibweise mit doppelter Negierung an.

## 📌 Wofür ist das nützlich?
- "Wie viele Kunden liegen über dem Durchschnitt?" - Kombination mit `AVERAGE`
- Überprüfung auf doppeltes Vorkommen

## 💡 Kreativer Einsatz - in dieser Lektion vorgezogen
*Wie lässt sich die Funktion clever kombinieren oder zweckentfremden?*
- Zähler bei Textkriterien mit Platzhaltern - wird gleich noch etwas genauer ausgeführt
- Abgleich zwischen zwei Listen

## 🔢 Beispiel
```excel
# Zählt wie oft die 10 im angegebenen Bereich vorkommt
=COUNTIF(A1:A15, 10)

# Zählt wie viele Zahlen größer als 10 sind
=COUNTIF(A1:A15, ">10")

# Abgleich einer Liste, gegen eine andere. Resultiert in einem Array.
=COUNTIF(A1:A15, B1:B15)

# Zählt, wie oft das Wort "test" im Bereich vorkommt (Not Case-Sensitive)
=COUNTIF(A1:A15, "*test*")
```

## Arbeiten mit Wildcards
In Excelfunktionen, die mit Wildcards arbeiten können, gibt es zwei verschiedene Möglichkeiten:

| Bezeichnung | Definition                         |
| ----------- | ---------------------------------- |
| \*          | sucht nach beliebig vielen Zeichen |
| \?          | sucht nach genau einem Zeichen     |

Bei der Suche mit Platzhaltern kommt es darauf an, wo die Platzhalterzeichen platziert werden.

```excel
# Sucht nach allen Vorkommnissen des Wort "test"
=COUNTIF(A1:A15, "*test*")

# Sucht nach allen Vorkommnissen, bei denen das Wort "test" am Anfang steht
=COUNTIF(A1:A15, "test*")

# Sucht nach allen Vorkommnissen, bei denen das Wort "test" am Ende steht
=COUNTIF(A1:A15, "*test")

# Sucht nach allen Vorkommnissen, mit "t" am Anfang, "st" am Ende und einem willkürlichen Buchstaben dazwischen
=COUNTIF(A1:A15, "t?st")

# Sucht nacht allen Vorkommen mit "t" am Anfang, gefolgt von einem willkürlichen Buchstaben, gefolgt von st und egal was am Ende
=COUNTIF(A1:A15, "t?st*")
```

Möchten wir nun in dem jeweiligen Bereicht wirklich nach einem Sternchen, einem Fragezeichen oder einer Tilde suchen, müssen wir dieses Zeichen maskieren. Dies geschieht ebenfalls mit einer Tilde, also:

| Bezeichnung | Definition                  |
| ----------- | --------------------------- |
| ~\*         | sucht nach dem Zeichen "\*" |
| ~\?         | sucht nach dem Zeichen "?"  |
| ~~          | sucht nach dem Zeichen "~"  |

In der Anwendung:
```excel
# Sucht nach jedem Vorkommen eines Sterns
=COUNTIF(A1:A15, "*~**)
```

## ⚠ Typische Fehlerquellen
- Excel lässt uns die Formeln nicht bestätigen
	- Fehler in Bedingung --> erwarteter Datentyp entspricht nicht dem übergebenen
	- Fehler in Bereichsangabe --> ArrayFORMEL wurde übergeben
		- Ein Array (`A1#`) kann übergeben werden
		- Die Formel, welches das Array produziert, kann nicht direkt übergeben werden

## 🧠 Denkstruktur hinter der Funktion
*Wie funktioniert diese Funktion auf struktureller Ebene?*
- Kann ebenfalls wunderbar als Grundlage für eine Bedingungsüberprüfung einer logischen Funktion genutzt werden
	- *"x wenn `COUNTIF` größer ist als y*
- Auswertung von Grenzbereichen
	- *"Ergebnis von`COUNTIF` darf y nicht überschreiten."*


___

# 📄 `SUMIF()`

## 🔹 Syntax
`=SUMIF(range, criteria, [sum_range])`

### Parameter

| Parameter    | Beschreibung                                | Pflichtangabe | erwarteter Datentyp                          |
| ------------ | ------------------------------------------- | ------------- | -------------------------------------------- |
| range        | Der Bereich welcher durchlaufen werden soll | Ja            | Bereich, (kein berechnetes) Array, Zelle     |
| criteria     | Bedingung, auf die überprüft wird           | Ja            | Bedingung, die in booleschem Wert resultiert |
| \[sum_range] | Bereich der summiert werden soll            | Nein          | Bereich, (kein berechnetes) Array, Zelle     |

## 🔍 In einfacher Sprache
*Was macht diese Funktion eigentlich – ohne Fachchinesisch?*
> Die `SUMIF`-Funktion summiert einen Bereich, nachdem dieser (oder ein anderer) auf eine bestimmte Bedingung überprüft wird. Bleiben wir bei der leichten Sprache und dem Beispiel der Kinder eines Klassenzimmers.
> Gehen wir in das Szenario einer geplanten Klassenfahrt und Geldern die für diesen Zweck eingesammelt werden sollen. Nun möchte man den Betrag summieren, den man schon erhalten hat. 
> Wir haben nun also zwei Bereiche/Spalten. Eine Spalte "Erhalten", die mit "Ja" oder "Nein" befüllt wird und die zweite Spalte mit dem Betrag, welcher gegeben wurde.
> `=SUMIF(Bereich_mit_ja_nein, "Ja", Bereich_mit_Beträgen` 

## 📌 Wofür ist das nützlich?
- Wird sehr nützlich, wenn man mehrere Kriterien hat, für die man einzelne Auswertungen braucht (Array oder Bereich als Bedingung)
- Summieren mit Ein-/Ausschlusskriterien

## 🔢 Beispiel
```excel
# Summiert die Zahl 3 so oft, wie sie im Bereich vorkommt
=SUMIF(A1:A15, 3)

# Summiert das Array in B1#, wenn in Array A1# die 1 steht.
=SUMIF(A1#, 1, B1#)

# Generiert ein Array in der Größe von B1# und gibt die Summe von C1# zurück, bei denen das jeweilige Kriterium von B1# in A1# zutrifft.
=SUMIF(A1#, B1#, C1#)
```

## ⚠ Typische Fehlerquellen
- Die gleichen wie bei der `COUNTIF`-Funktion

## 🧠 Denkstruktur hinter der Funktion
Wie funktioniert diese Funktion auf struktureller Ebene?*
- Die Funktion `SUMIF()` erlaubt die zielgerichtete Summierung innerhalb eines Bereichs basierend auf vordefinierten Bedingungen – entweder auf den selben oder einen anderen Bereich bezogen.