
# 📄 `TEXTJOIN()`

## 🔹 Syntax
`=TEXTJOIN(delimeter, ignore_empty, text1, [text2])`

### Parameter

| Parameter    | Beschreibung                                                                   | Pflichtangabe | erwarteter Datentyp         |
| ------------ | ------------------------------------------------------------------------------ | ------------- | --------------------------- |
| delimeter    | Trennzeichen, welches verwendet werden soll, geschrieben in Anführungsstrichen | Ja            | Text (" ")                  |
| ignore_emtpy | leere Zellen werden ignoriert                                                  | Ja            | bool                        |
| text1        | Erster Wert, der verbunden werden soll                                         | Ja            | Text, Zelle, Bereich, Array |
| \[text2]     | Zweiter Wert, der verbunden werden soll                                        | Nein          | Text, Zelle, Bereich, Array |

## 🔍 In einfacher Sprache
*Was macht diese Funktion eigentlich – ohne Fachchinesisch?*
> `TEXTJOIN` verbindet mehrere Werte mit einem Trennzeichen zwischen den verschiedenen Werten. Ähnliche Funktionen sind `CONCATENATE` und `CONCAT` (welches der Nachfolger von `CONCATENATE` ist). Der Unterschied dieser Textfunktionen besteht darin, dass man bei `TEXTJOIN`, im Gegensatz zu den anderen beiden Funktionen, leere Werte ignorieren kann und ein Trennzeichen angeben kann/muss.

## 📌 Wofür ist das nützlich?
Wir können die `TEXTJOIN`-Funktion verwenden, um beispielsweise auf Reportings oder Dashboards verschiedene Werte in einem Satz zusammen zu fassen. In dieser Rolle eher auf Frontend-Ebene eingesetzt.
Sie eignet sich aber auch wunderbar dazu, um im "Verborgenen" zu arbeiten, Backend, wenn man so will, um beispielsweise verschiedene Daten so zusammenzuführen, dass sie in einem weiteren Schritt anhand von Trennzeichen verarbeitet werden können. Manchmal benötigt um einen Workaround aus importierten Daten zu schaffen.

## 🔢 Beispiel
```excel
# Verbindet alle gefüllten Zellen (ignore_empty = TRUE) mit einem Komma als Trennzeichen
=TEXTJOIN(", ", TRUE, A1:A10)

# Verbindet das angegebene Array ohne Trennzeichen
=TEXTJOIN("", TRUE, A1#)

# Verbindet mehrere Bereiche und Arrays miteinander. Zeichnet auch leere Zellen ab. Verwendet ein Semikolon als Trennzeichen.
=TEXTJOIN("; ", FALSE, A1:A10, B5:D16, F60#)
```

## 📊 Was kommt dabei raus?
| Eingabe-Daten                                       | Formel                          | Ergebnis der Funktion              |
| --------------------------------------------------- | ------------------------------- | ---------------------------------- |
| A1:A10 mit 1 bis 10 gefüllt                         | `=TEXTJOIN(", ", TRUE, A1:A10)` | 1, 2, 3, 4, 5, 6, 7, 8, 9, 10      |
| A5 mit Arrayformel welche eine 1-9 Sequenz erstellt | `=TEXTJOIN("", TRUE, A5#) `     | 123456789                          |
| A1:B5 mit 1 bis 5 nur in Spalte A                   | `=TEXTJOIN(", ", FALSE, A1:B5)` | 1, , , 2, , , 3, , , 4, , , 5, , , |
| A1:B5 mit 1 bis 5 nur in Spalte A                   | `=TEXTJOIN(", ", TRUE, A1:B5)`  | 1, 2, 3, 4, 5                      |

## 💡 Kreativer Einsatz
*Wie lässt sich die Funktion clever kombinieren oder zweckentfremden?*
In Kombination mit Funktionen wie `VSTACK` und iterierenden Funktionen wie `MAP` oder `BYROW` kann relativ leicht ein Array geschaffen werden, welches von anderen Funktionen problemlos reihenweise verarbeitet werden kann.

## ⚠ Typische Fehlerquellen
\#VALUE!-Error, wenn die Länge des Texts (32.767 Zeichen) überschreitet. Ansonsten eigentlich nur eine Fehlerübergabe einer unterliegenden Funktion.

## 🧠 Denkstruktur hinter der Funktion
Wie funktioniert diese Funktion auf struktureller Ebene?*

Folgende Tabelle gegeben:

|     | A     | B           | C        | D        | E        |
| --- | ----- | ----------- | -------- | -------- | -------- |
| 1   | test1 | 0.344765345 | 0.091811 | 0.899268 | 0.503071 |
| 2   | test2 | 0.490003371 | 0.909493 | 0.398405 | 0.657988 |
| 3   | test3 | 0.527045385 | 0.588062 | 0.524196 | 0.351201 |
| 4   | test4 | 0.506654184 | 0.300274 | 0.951361 | 0.760924 |
| 5   | test5 | 0.703317926 | 0.032869 | 0.231977 | 0.441773 |
| 6   | test6 | 0.211996883 | 0.664286 | 0.609863 | 0.928345 |
| 7   | test2 | 0.498236924 | 0.05159  | 0.125225 | 0.095561 |
| 8   | test7 | 0.450813022 | 0.108325 | 0.96764  | 0.940855 |
| 9   | test8 | 0.581855245 | 0.482966 | 0.004661 | 0.04229  |
| 10  | test9 | 0.498685823 | 0.386813 | 0.702975 | 0.647411 |

Unser Ziel ist es nun diese Tabelle in ein einziges Array zu wandeln, welches sich automatisch erweitert und die jeweiligen Namen zu den Zeilen mit ausgibt.

```excel
=LET(
v_names, UNIQUE(A1:A10),

HSTACK(v_names,MAP(v_names, LAMBDA(x,TEXTJOIN(", ",TRUE,XLOOKUP(x,A1:A10,B1:E10,,0))))))
```

Nun haben wir die Möglichkeit via `CHOOSECOLS` auf die 2(!) Spalten des Arrays zuzugreifen und weiter zu verarbeiten.