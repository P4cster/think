
# 📄 `IF()`

## 🔹 Syntax
`=IF(logical_test, value_if_true, [value_if_false])`

### Parameter

| Parameter         | Beschreibung                                                             | Pflichtangabe | erwarteter Datentyp      |
| ----------------- | ------------------------------------------------------------------------ | ------------- | ------------------------ |
| logical_test      | Die Bedingung auf die überprüft wird. Resultiert in `TRUE` oder `FALSE`. | Ja            | bool                     |
| value_if_true     | Was soll passieren, wenn logical_test `TRUE` zurück gibt?                | Ja            | Formel, Wert, Text, etc. |
| \[value_if_false] | Was soll passieren, wenn logical_test `FALSE` zurück gibt?               | Nein          | Formel, Wert, Text, etc. |

## 🔍 In einfacher Sprache - etwas ausführlicher

Die IF-Formel ist eine grundlegende Formel die dann zum tragen kommt, wenn man zwei verschiedene "Ausgänge" von einem Ereignis abhängig machen möchte. Es handelt sich hierbei um eine der sogenannten "logischen Funktionen".
Ihr könnt euch das wie folgt vorstellen:

"Wenn meine Freundin heute abend Zeit hat, gehe ich mit ihr essen, ansonsten bleibe ich zuhause."

Hierbei ist die Variable, die sich verändern kann die Zeit der Freundin. Die beiden anschließenden Möglichkeiten sind abhängig von dieser Entscheidung. 

In Excel sähe das ganze dann so aus:

```
=IF(Freundin=Zeit, essen gehen, zuhause bleiben)
```

Das ganze in der Syntax ist wie folgt beschrieben:

```
=IF(Wahrheitstest, was passiert wenn wahr, was passiert wenn falsch)
```

Der `Wahrheitstest` ist dabei in der Regel (fast immer) ein Vergleich, also `=`, `<`, `>`, `<=`, `>=` oder `<>`. Alle diese Bedingungen werden für Excel in ein "stimmt" oder "stimmt nicht" aufgelöst. Mit anderen Worten ein `TRUE` oder ein `FALSE` und dementsprechend wird dann das jeweilige "was passiert wenn wahr" oder "was passiert wenn falsch" ausgelöst.

Also:

```
=IF(Freundin=Zeit, essen gehen, zuhause bleiben)

=IF(Freundin hat Zeit (TRUE), essen gehen, -)

=IF(Freundin hat keine Zeit (FALSE), - , zuhause bleiben)
```

### Verschachtelte `IF`-Funktion
Das Thema der IF-Funktionen kann man jetzt noch auf die Spitze treiben und anfangen zu verschachteln. Bleiben wir bei unserem Beispiel und erweitern um die Auswahl des Essens, aber nur dann, wenn unsere Freundin Zeit hat, um mit uns essen zu gehen.

Hierfür legen wir die Bedingung fest:

Wenn wir Lust auf Pizza haben, gehen wir in ein italienisches Restaurant, ansonsten mexikanisch.

```
=IF(Freundin=Zeit, IF(Lust = Pizza, italienisch, mexikanisch), zuhause bleiben)
```

In unserem Beispiel haben wir nun unsere "was passiert wenn wahr" gegen die Wahl des Essens ausgetauscht. Also:

```
=IF(Freundin hat Zeit (TRUE), IF(Lust = Pizza, italienisch, mexikanisch), - )
```

Unsere Freundin hat Zeit, also gehen wir was essen und die Option zuhause zu bleiben verfällt. Jetzt kommen wir dementsprechend an die Entscheidung ob wir Lust auf Pizza haben:

```
=IF(Freundin hat Zeit (TRUE), IF(wir haben Lust auf Pizza (TRUE), italienisch, -), - )
```

Also:

```
=IF(Freundin hat Zeit (TRUE), IF(wir haben Lust auf Pizza, italienisch, -), - )

=IF(Freundin hat Zeit, italienisch, -)
```

Und nein, ich plane meine Dates nicht mit Excel. So gut bin ich dann auch nicht.

### Mehrfach verschachtelte `IF`-Funktion
Mehrfach verschachtelte `IF`-Funktionen sind natürlich ebenfalls möglich. Also eine `IF`-Funktion, in der eine `IF`-Funktion, in der eine `IF`-Funktion, in der eine `IF`-Funktion, in der eine, usw. eingesetzt wird. 
Wenn wir dabei mit numerischen Werten arbeiten, müssen wir darauf achten, dass wir die Bedingungsprüfungen logisch absteigend/aufsteigend aufbauen. Das heißt, dass wir erst auf den größten/kleinsten Wert überprüfen, dann auf den nächstkleineren/nächstgrößeren, usw.

```
# festgelegte Variable:
varZahl = 25

IF(
varZahl >= 40, "Die Variable ist größer als 40.", IF(
	varZahl >= 30, "Die Variable ist größer als 30.", IF(
		varZahl >= 20, "Die Variable ist größer als 20.", IF(
			varZahl >=10, "Die Variable ist größer als 10.", IF(
				varZahl < 10, "Die Variable ist kleiner als 10."
)))))
```

Wenn unsere Zahl größer oder gleich 40 ist, hört die mehrfach verschachtelte `IF`-Funktion bei der ersten Bedingung auf weiter zu überprüfen und gibt den Text "Die Variable ist größer als 40." zurück. Sollte da nicht der Fall sein, wird die nächste Überprüfung getriggert. Mit anderen Worten: Es wird so lange überprüft, bis die erste Bedingung ein `TRUE` zurück gibt.

Diesem Muster folgend löst sich die Formel also wie folgt auf.
```
# festgelegte Variable:
varZahl = 25

=IF(
varZahl >= 40, "Die Variable ist größer als 40.", IF(
	varZahl >= 30, "Die Variable ist größer als 30.", IF(
		varZahl >= 20, "Die Variable ist größer als 20.", IF(
			varZahl >=10, "Die Variable ist größer als 10.", IF(
				varZahl < 10, "Die Variable ist kleiner als 10."
)))))

=IF(
FALSE, -, IF(
	varZahl >= 30, "Die Variable ist größer als 30.", IF(
		varZahl >= 20, "Die Variable ist größer als 20.", IF(
			varZahl >=10, "Die Variable ist größer als 10.", IF(
				varZahl < 10, "Die Variable ist kleiner als 10."
)))))

=IF(
FALSE, -, IF(
	FALSE, -, IF(
		varZahl >= 20, "Die Variable ist größer als 20.", IF(
			varZahl >=10, "Die Variable ist größer als 10.", IF(
				varZahl < 10, "Die Variable ist kleiner als 10."
)))))

=IF(
FALSE, -, IF(
	FALSE, -, IF(
		TRUE, "Die Variable ist größer als 20.", -
)))


=IF(
FALSE, -, IF(
	FALSE, -, "Die Variable ist größer als 20."
))

=IF(
FALSE, -, "Die Variable ist größer als 20."
)

="Die Variable ist größer als 20."
```

### IFERROR und IFNA

Eine `IFERROR` und `IFNA` funktionieren letztlich nach dem gleichen Prinzip wie eine normale `IF` Funktion. Hier ist der Wahrheitstest allerdings direkt in die Funktion integriert und überprüft, ob bei der übergebenen Formel ein allgemeiner Fehler (`IFERROR`), oder spezieller, ein NV-Fehler (`IFNA`) entsteht.

"Wenn die Formel einen Fehler ausgibt, dann tue bitte xyz, ansonsten führe die Formel aus."

```
=IFERROR(10+10,"Hier ist ein Fehler! Bitte überprüfe die Formel!")
```

Wenn die Berechnung 10 + 10 einen Fehler ausgibt, dann gibt die `IFERROR` den Satz "Hier ist ein Fehler! Bitte überprüfe die Formel!" aus. Wenn die Berechnung keinen Fehler ausgibt, wird das Ergebnis 20 angezeigt.

```
=IFERROR(10+10,"Hier ist ein Fehler! Bitte überprüfe die Formel!")
=20
```

Provozieren wir nun einen Fehler, indem wir einen Text mit einer Zahl addieren möchten:

```
=IFERROR(10+"abc","Hier ist ein Fehler! Bitte überprüfe die Formel!")
="Hier ist ein Fehler! Bitte überprüfe die Formel!"
```

Hier versuchen wir 10 mit abc zu addieren. Geht logischerweise nicht. Also wird ein `#VALUE` Fehler in Excel ausgegeben. Durch den Fehler wird unsere `IFERROR`-Formel getriggert und gibt uns den eingegebenen Text zurück.

> [!Info]
> Während `IFERROR` und `IFNA` Formeln durchaus ihren Reiz haben, können sie auch "unvorhersehbares" Verhalten auslösen. Das geschieht insbesondere dann, wenn anstelle des Fehlers eine 0 ausgegeben wird (`=IFERROR(Formel die Fehler generiert, 0)`). Mit einer 0 können wir weitere Berechnungen ausführen, mit einem Fehler nicht. Wenn wir also Spalten summieren, die eigentlich nur Fehler enthalten, anstelle eines Fehlers aber eine 0 ausgeben, erhalten wir als Ergebnis 0, obwohl unsere komplette Tabelle fehlerhaft ist.


## 📌 Wofür ist das nützlich?
- Immer dann, wenn eine einfache logische Bedingung geprüft und ein Rückgabewert auf Grundlage des Ergebnis zurück gegeben werden muss.
- Wir sind zwar auf mehrfach verschachtelt eingegangen, da gibts aber einfachere und besser lesbare Möglichkeiten, wie z.B. die `IFS`, die direkt auf mehrere Bedingungen überprüft.

## 🔢 Beispiel
```excel
=IF(varDatum < TODAY(), "Bitte überprüfe das Reportingdatum!", Berechnung)

# Formel im Namensmanager anlegen: Name = forGroesserAls
=LAMBDA(x, IF(
x >= 40, "Die Variable ist größer als 40.", IF(
	x >= 30, "Die Variable ist größer als 30.", IF(
		x >= 20, "Die Variable ist größer als 20.", IF(
			x >=10, "Die Variable ist größer als 10.", IF(
				x < 10, "Die Variable ist kleiner als 10."
)))))))
--> Funktionsaufruf über
=forGroesserAls(20) --> "Die Variable ist größer als 20."

# Bedingungsüberprüfung mit Formel, die direkt einen booleschen Wert zurück gibt
=IF(ISNUMBER(15), "Es handelt sich um eine Zahl.", "Es handelt nicht um eine Zahl.")

=IF(20, 1, 0)
--> resultiert in 1, da 20 als TRUE interpretiert wird


```

## 💡 Kreativer Einsatz
*Wie lässt sich die Funktion clever kombinieren oder zweckentfremden?*
- Die Kombinationsmöglichkeit der `IF`-Funktion und seiner Geschwister ist schier endlos. Aufgrund ihrer Natur tragen logische Funktionen einen großen Teil zu einem Systemaufbau bei. Sie sind meistens an entscheidungskritischen Stellen innerhalb eines Modells, da sie genau das machen: Eine Entscheidung treffen.
- Es sei ebenfalls darauf hingewiesen, dass die logischen Funktionen jede andere Zahl als 0 als `TRUE` auswerten, positive und negative. 

## ⚠ Typische Fehlerquellen
- Falsche Bedinungsprüfungen (alles was nicht in `TRUE` oder `FALSE` resultiert), lösen einen \#VALUE-Error aus.

## 🧠 Denkstruktur hinter der Funktion
*Optional: Wie funktioniert diese Funktion auf struktureller Ebene?*
Die `IF`-Funktion, sowie die Verschachtelung, zu verstehen sind wegweisend für komplexere Modelle. Vieles, was man in einem systematisch aufgebautem Modell macht, ist auf bedingte Logik zurück zu führen. Mit logischen Funktionen erstellt man die ersten "echten" Synapsen seines Modells.

