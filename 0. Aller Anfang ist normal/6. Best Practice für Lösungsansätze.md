# Modellaufbau

Das abschließende Learning dieses Kapitels. Hier bündeln wir die zuvor vermittelten Inhalte und bringen sie in einen strukturellen Zusammenhang.

Nochmal in der Übersicht:

| Bezeichnung      | Definition                                                                           |
| ---------------- | ------------------------------------------------------------------------------------ |
| Normalisierung   | Grundlage für strukturierten Modellaufbau                                            |
| Datentypen       | Wie werden die Daten gespeichert?                                                    |
| Zellformatierung | Wie werden die Daten angezeigt?                                                      |
| Tabellen         | Strukturierter, benannter, eindeutiger Zusammenschluss von Zellen                    |
| Arrays           | Dynamisch generierter Zusammenschluss von Zellen, abgespeichert als einzelnes Objekt |
| Namensmanager    | Mehr oder weniger eine Möglichkeit der Navigation                                    |

Diese Elemente bilden gemeinsam das Fundament eines modularen, skalierbaren und wartbaren Excel-Modells.  
Und genau darum geht es: nicht nur eine Lösung zu bauen – sondern ein **System**, das auf Struktur, Klarheit und Wiederverwendbarkeit basiert.

Beginnen wir mit dem Aufbau unseres Modells.  
Prinzipiell genügt zunächst ein grob umrissener Plan, da ein gutes Excel-Modell in seiner Architektur modular und erweiterbar konzipiert sein sollte.  
In der Praxis zeigt sich jedoch, dass ein durchdachtes Vorabkonzept Zeit spart, Fehler vermeidet und spätere Anpassungen erleichtert.

Für einen sauberen Aufbau stellen wir uns drei zentrale Fragen:

## Warum?
Was ist der Zweck des Modells?  
Welche Aufgaben soll es erfüllen? Wer sind die Nutzer?  
Welche Prozesse sollen automatisiert oder vereinfacht werden?  
Gibt es messbare Effekte – zum Beispiel durch Zeitersparnis, Fehlervermeidung oder bessere Entscheidungsgrundlagen?

Hier ist ganz wichtig zu verstehen: Nur weil Excel das kann, heißt das noch lange nicht, dass du es auch dafür verwenden solltest! 
Natürlich kann man ein komplettes Datenbankmodell in Excel gestalten. Sollte man es? Auf gar keinen Fall. Hierfür eignen sich andere Systeme und Sprachen wie SQL wesentlich besser. Du würdest ja auch keine Geburtstagskarte auf Stein meißeln, "nur weil es geht". 

Im Kern sollte man sich immer Gedanken darüber machen, ob es Sinn macht Excel zu verwenden, oder ob es **bessere** Alternativen gibt. Wenn es nur eine **andere** Alternative ist, kann man getrost Excel verwenden, wenn es aber eine **bessere** ist, sollte man auf diese zurückgreifen. 


## Wie?
- Welche Architektur wählen wir?  
	- Eine zentrale Datei (Monolith), in der Daten, Logik und Reporting zusammengeführt sind?  
	- Ein modulares System mit separaten Daten-, Berechnungs- und Reporting-Einheiten?  
- Arbeiten wir mit internen Daten oder binden wir externe Quellen an?
- In welcher Form liegen unsere Daten vor? Andere Exceldatei, CSV, Onlineabruf, Datenbank, PDF, etc...
- Welche Rolle spielen Power Query, Datenmodelle, Tabellenlogik, Dropdowns, strukturierte Bezüge, Arrays und benannte Bereiche?

Die Architektur ist mitunter das Einzige, was am Ende nicht mehr verändert werden kann. Zumindest nicht ohne enormen Zeit- und Ressourcenaufwand. Alleine daher ist es wichtig in diesen Aufbauprozess genügend Zeit und Hirnschmalz mit einfließen zu lassen. Nichts ist unbefriedigender als eine vorhandene Struktur umzudenken und neu implementieren zu müssen.


## Was?
Die konkrete Umsetzung ergibt sich aus den beiden vorherigen Fragen.  
Welche Tabellen werden benötigt? Welche Variablen, welche Namenskonventionen, welche Datenstruktur?  
Welche Vorlagen oder Funktionsblöcke lassen sich vorbereiten?  
Was muss an welcher Stelle zugänglich, erweiterbar oder wartbar sein?

Hier geht man etwas granularer an die Thematik der Datengestaltung. Man macht sich Gedanken über die späteren Funktionen des Modells, wie diese präsentiert werden sollen, welche globalen Variablen existieren, wie die zugrundeliegenden Daten normalisiert werden sollen/können, etc.
Während sich die anderen beiden Fragen mit der Seele und dem Skelett auseinandergesetzt haben, geht es hier nun um die erste Implementierung von Hirn und Intelligenz.

---

Ein gutes Modell ist keine Sammlung von Formeln – sondern ein logisches System mit Struktur, Klarheit und Zielrichtung.


# Formelanalyse und -aufbau

## Grundlagen der Funktionskomposition

Nehmen wir ein Beispiel, das bereits ein bisschen komplexer wirkt: eine klassische INDEX-MATCH-Kombination. Sie wird verwendet, um in einem zweidimensionalen Bereich einen Schnittpunkt zu ermitteln – also Zeile x Spalte = Zelle.

```
=INDEX(varBereich, MATCH(15, A1:A300), MATCH("Umsatz", A1:G1))
```

Oder in Klartext:  
Wir möchten in unserem definierten Bereich `varBereich` den Wert anzeigen, der sich in der Zeile des Kunden mit der ID `15` und in der Spalte mit der Überschrift `"Umsatz"` befindet.

## Analyse – Von innen nach außen

Wie in der Mathematik üblich: Man löst von innen nach außen.

### 1. Innerste Elemente

```
=MATCH("Umsatz", A1:G1)
```
→ Sucht `"Umsatz"` in der ersten Zeile und gibt die Spaltennummer zurück (z. B. `3`).

```
=MATCH(15, A1:A300)
```
→ Sucht `15` in der ersten Spalte und gibt die Zeilennummer zurück (z. B. `122`).

Wenn wir diese Werte einsetzen:

```
=INDEX(varBereich, 122, 3)
```

Und wenn `varBereich` der Bereich `A1:G300` ist:

```
=INDEX(A1:G300, 122, 3)
```

Excel gibt den Wert zurück, der in der 122. Zeile der dritten Spalte dieses Bereichs steht.

## Aufbau – Vom Gerüst zur Komposition

Der strukturelle Aufbau folgt dem umgekehrten Prinzip:
- Zuerst überlegen wir, welche Funktion am Ende das Ergebnis erzeugen soll (z. B. `INDEX()`),
- dann überlegen wir, welche Parameter diese Funktion benötigt,
- anschließend bauen wir die Teilfunktionen schrittweise auf.

### Denken in „Hilfsspalten“

Insbesondere bei komplexeren Formeln empfiehlt es sich, die einzelnen Bestandteile zunächst separat zu prüfen – auch als rudimentäres Debugging-System.  
Das erleichtert Fehleridentifikation und strukturiert gleichzeitig das eigene Denken.

- Spalte 1: `=MATCH("Umsatz", A1:G1)`  
- Spalte 2: `=MATCH(15, A1:A300)`  

Danach: Zusammenbau zur fertigen `INDEX()`-Formel, also:
```
=INDEX(A1:G300, MATCH(15, A1:A300), MATCH("Umsatz", A1:G1))
```

### Dynamik durch Auslagerung von Parametern

Wenn wir nun z. B. die gesuchte ID in `I1` und die Spaltenüberschrift in `I2` platzieren, wird die Formel dynamisch:

```
=INDEX(A1:G300, MATCH(I1, A1:A300), MATCH(I2, A1:G1))
```

So entsteht ein echtes Modell – kein fest verdrahteter Formelblock – der durch Benutzereingaben in `I1` & `I2` dynamisch angepasst werden kann.

## Und wenn man das weiterdenkt...

Dann landet man irgendwann bei Modellen wie diesem hier:

```
=LET(
 v_shareclass, CUBESET("ThisWorkbookDataModel","[PNL_PLBreak].[Share_class].children"),
 v_filter, MAP(A3:A11, LAMBDA(x, FILTER(v_shareclass, v_shareclass=x))),
 v_cuberanked, CUBERANKEDMEMBER("ThisWorkbookDataModel", v_shareclass, SEQUENCE(CUBESETCOUNT(v_shareclass))),
 v_sort, SORTBY(v_cuberanked, XMATCH(v_cuberanked, FILTER(varScSheet1, varScSheet1<>""))),
 v_map, MAP(varScSheet1, LAMBDA(x, FILTER(v_sort, v_sort=x))),
 
 MAP(v_map, LAMBDA(x, CUBEVALUE("ThisWorkbookDataModel", CUBEMEMBER("ThisWorkbookDataModel", "[Measures].[Sum of Balance]"), x)))
)
```

Ein Konstrukt, das auf funktionaler Modellierung, iterativer Verarbeitung, semantischer Strukturierung und datengestützter Aggregation beruht. Es wirkt zunächst überfordernd – aber es folgt exakt den gleichen Prinzipien wie die kleine `INDEX()`-Formel von oben.

Formeln bauen heißt Denken in Modulen.  
Wer systematisch von innen nach außen analysiert und von außen nach innen konstruiert, erreicht schnell ein höheres Komplexitätsniveau – ohne den Überblick zu verlieren.

Denn am Ende ist auch eine `LAMBDA-CUBE-MAP-Formel` nur ein Index-Match mit sehr, sehr vielen Muskeln.


# Error-Handling

Kommen wir zu einem Thema, welches ein gutes System zu einem großartigen werden lässt:

Error-Handling.

An Error-Handling kann man direkt erkennen, wie viel Ahnung eine Person im Aufbau eines Modells und mit Excel hat. Um ein solches zu implementieren gibt es verschiedene Möglichkeiten:

- Formelintern (Error-Handling ist in die Syntax der Funktion eingebaut - bspw.`XLOOKUP`)
- `IFERROR` - Deklariert was passieren soll, wenn die Funktion egal welchen Fehler ausgibt
- `IFNA` - Deklariert was passieren soll, wenn die Funktion einen \#NA-Fehler ausgibt.
- `ERROR.TYPE` in `LAMBDA` und Namensmanager - vermutlich eine der komplexeren, aber eleganteren Lösungsvarianten

Gehen wir die einzelnen Punkte durch.

## Formelintern
Manche der neueren Formeln haben ein Error-Handling schon eingebaut. So fragt die `XLOOKUP` durch einen optionalen Punkt, was geschehen soll, wenn kein Wert mit ihr gefunden werden konnte. Dabei kann man eine Zahl, einen booleschen Wert, einen Text oder was auch immer zurück geben. 
Das ist die erste und direkteste Art des Error-Handlings.

## `ISNA`, `ISERR`, `ISERROR` / `IFNA`, `IFERROR`

Bei den drei "IS"-Formeln handelt es sich um Funktionen, die einen booleschen Wert, also `TRUE` oder `FALSE` zurück geben. 
- `ISNA` prüft, ob der \#N/A-Fehler zurück gegeben wird und gibt `TRUE` zurück, wenn dies der Fall ist
- `ISERR` prüft ob ein andere Fehler **außer** der \#N/A-Fehler zurück gegeben wird
- `ISERROR` prüft ob irgendein, egal welcher, Fehler zurück gegeben wird.

Um das ganze abzurunden, können wir rein theoretisch hingehen und die jeweiligen `IS...`-Formeln mit einer `IF()` umschließen. Alleine hierdurch wird eine erste Form von Error-Handling betrieben. Möchten wir bei einem Fehler beispielsweise den Text "Hier ist ein Fehler!" ausgeben lassen, sähe eine `IF(ISERROR())` wie folgt aus:

```
=IF(ISERROR(Formelberechnung), "Hier ist ein Fehler!", Formelberechnung)
```

Das ist prinzipiell ein valider Ansatz, wurde durch die Formel `IFERROR()` stark vereinfacht. Das gleiche Ergebnis wie mit der `IF`-Formel erhält man durch:
```
=IFERROR(Formelberechnung, "Hier ist ein Fehler!")
```

Wir sehen also, dass wir, erstens, nur mit einer "Fehlerformel" arbeiten (`IFERROR` <>`IF(ISERROR())`) und, zweitens, unsere Formelberechnung nicht wiederholen müssen. Wie schon erwähnt, das Ergebnis ist das gleiche. 

## `ERROR.TYPE` mit `SWITCH`, `LAMBDA`, `IFERROR` und Namensmanager
Zugegeben, das ist in den meisten Fällen absoluter Overkill, trifft aber genau den Nerv von dem, wo dieses Repo hin soll. Am Ende wollen wir ja keine File "nur für uns" machen, sondern ein System erschaffen, welches mit "fremden" Benutzereingaben umgehen, Fehler ordentlich auslesen und Berechnungen möglichst effektiv, einfach, schnell und redundant durchführen kann.

Wir gehen an dieser Stelle nicht darauf ein, wie man diese Formel erstellt, da sie, ebenso wie die `LET`-Formel weiter oben, in keinster Weise das Level widerspiegelt, auf dem wir uns am Stand Jetzt befinden und ich eindeutig niemandem Angst davor machen möchte, weiter zu lesen.
Grob angerissen: Jeder einzelne Fehlerwert bekommt durch die `ERROR.TYPE` eine eindeutige Nummer zugewiesen, die im weiteren Verlauf mit `SWITCH` verarbeitet werden kann. 
Alles weitere in späteren Kapiteln.